/* MDXMKRUN REXX      Run the three-headed pipe against the records  */
/*                    Author: Rob van der Heij, 4 May 2017           */

/* The specification items for the spec stage were generated by the  */
/* MDXMKFLD stage. This expects a SPEC stage with two extra streamid */
/* to separate the names and values.                                 */

/* For JSON we quote the key and have ':' between key and value.     */
/* For InfluxDB write format, we would not quote the key and use '=' */


parse arg specs                       /* Arguments for spec stage    */

'callpipe (end \ name MDXMKRUN.REXX:6)',
   '\ *:',
   '| s: spec' specs,
   '| copy',
   '| k: spec ,", 1 1-* n ," :, n',   /* Format name                 */
             'select 1 1-* nw',       /* .. and add value            */
   '| *:',
   '\ s.str:',                        /* Format string values        */
   '| j: faninany',
   '| strip',
   '| strip trailing 00',             /* Clean up for display        */
   '| unicode \\ "',                  /* Also escape \ and "         */
   '| spec ,", 1 1-* n ,", n',
   '| i: faninany',
   '| k:',
   '\ s.num:',                        /* Format numeric values       */
   '| strip',                         /* To save some room           */
   '| i:',
   '\ s.dec:',
   '| strip',
   '| i:',
   '\ s.ptr:',
   '| pdwrdstr ',
   '| j:',

error: return rc * ( rc <> 12 )
